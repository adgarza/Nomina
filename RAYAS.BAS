10 'SAVE "RAYAS"
20 ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' '
30 ' RAYAS                                                           '
40 ' Programa para dar de alta y modificar datos de los pagos hechos '
50 ' a los trabajadores del sistema de N¢minas de GMD Microsistemas  '
60 ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' '
70 'CHECA si fu‚ llamado desde el men£ principal
80 IF EMPRESA$ = "" THEN RUN "nomina"
90 'Inicializa los datos
100     DEFINT A-Z
110    OPTION BASE 1: DIM C(6, 9)
120    FOR I = 1 TO 6
130      FOR J = 1 TO 9
140        READ C(I, J)
150    NEXT J, I
160    OPEN "I", 1, "nomina.ini"
170      INPUT #1, EMPRESA$, CE
180    CLOSE
190   FALSE = FALSE: TRUE = NOT FALSE
200   CAMPO = 1
210   MASKN$ = "\                            \"
220   MASKP$ = "\        \"
230   MASKD$ = "$$###,###.##"
240   MASKO$ = "###"
250   ESC$=CHR$(27): ENT$ = CHR$(13): F10$ = CHR$(0) + "D": F9$ = CHR$(0) + "C"
260   F8$=CHR$(0)+"B": F7$=CHR$(0)+"A": F6$= CHR$(0) + "@": F2$ = CHR$(0) + "<"
270   FA$=CHR$(0)+"H": FB$=CHR$(0)+"P": FI$= CHR$(0) + "K": FD$ = CHR$(0) + "M"
280   PA$=CHR$(0)+"I": PB$=CHR$(0)+"Q": HO$= CHR$(0) + "G": EN$ = CHR$(0) + "O"
290   GOSUB 1040
300 :
310 'Programa Principal
320  OPEN "R",1,"SEM",2:FIELD #1,2 AS SEM$:IF LOF(1)/2>0 THEN GET #1,1:SEM=CVI(SEM$):CLOSE:KILL "SEM":FLAG=TRUE:GOSUB 4000:GOTO 340 ELSE CLOSE:KILL "SEM":FLAG=FALSE
330  GOSUB 3940 'PideSemana
340  REG1 = 1: LIN = 4
350  INICIO = 1: IF TR <= 20 THEN FIN = TR ELSE FIN = 20
360  FINAL = FALSE
370  GOSUB 640: GET #1, REG1: CAMPO = 1: QUITA = FALSE: GOSUB 1330: GOSUB 1160: GOSUB 1460: GOSUB 3390
380  'DO
390    M$ = " Seleccionar ": GOSUB 1220
400    GOSUB 900 'Pulsar una tecla
410    IF T$ = FA$ THEN 1540'Arriba
420    IF T$ = FB$ THEN 1610'Abajo
430    IF T$ = FD$ THEN 1710'Derecha
440    IF T$ = FI$ THEN 1780'izquierda
450    IF T$ = PA$ THEN 1850'PgUp
460    IF T$ = PB$ THEN 1930'PgDn
470    IF T$ = HO$ THEN 2010'Home
480    IF T$ = EN$ THEN 2060'End
490 '
500    IF T$ = F9$ THEN 3010 'Baja
510    IF T$ = F8$ THEN T$="": GOTO 2810 'Busca Nombre
520    IF T$ = F7$ THEN 2890 'Imprime
530    IF T$ = F2$ THEN 2510 'N£meros
540    IF (T$=>"0" AND T$<="9") THEN 2510 'N£mero
550    IF T$ >= "A" AND T$ <= "Z" THEN 2810 'Busca Nombre
560    IF T$ >= "a" AND T$ <= "z" THEN 2810 'Busca Nombre
570    IF T$ = "¥" OR T$ = "¤" THEN T$="¥": GOTO 2810 'Busca Nombre
580    IF T$ = ESC$ THEN FINAL = TRUE ELSE BEEP
590  IF NOT FINAL THEN 380
600  CLOSE : RESET
610  RUN "nomina"
620 END
630 :
640  'SUB PresentaLineas
650   M$ = " Aguarde ": GOSUB 1220
660   L = 4
670   COLOR C(CE, 1), C(CE, 2)
680   FOR I = INICIO TO FIN
690     GET #1, I
700     LOCATE L, 1
710     COLOR C(CE, 3), C(CE, 4)
720     PRINT USING MASKO$; I;
730     LOCATE L, 4
740     COLOR C(CE, 1), C(CE, 2)
750     PRINT USING MASKN$; NOMBRE$;
760     LOCATE L, 36
770     PRINT USING MASKP$; PUESTO$;
780     LOCATE L, 49
790     PRINT USING MASKO$; CVI(DIAS$);
800     LOCATE L, 55
810     PRINT USING MASKO$; CVI(HE$);
820     LOCATE L, 60
830     PRINT USING MASKO$; CVI(HS$);
840     LOCATE L, 67
850     PRINT USING MASKD$; CVS(TOTAL$);
860     L = L + 1
870   NEXT
880 RETURN
890 :
900 'SUB PulseUnaTecla
910  T$ = INKEY$: IF T$ = "" GOTO 910
920 RETURN
930 :
940 'SUB TextoAyuda
950  LOCATE 25, 1
960  IF LEN(M$) >= 80 - (LEN(EMPRESA$) + 3) THEN MASK$ = SPACE$(80) ELSE MASK$ = SPACE$(80 - (LEN(EMPRESA$) + 3)) + "³ " + EMPRESA$ + " "
970  COLOR C(CE, 3), C(CE, 4)
980  PRINT MASK$;
990  LOCATE 25, 2
1000  COLOR C(CE, 9)
1010  PRINT M$;
1020 RETURN
1030 :
1040 'SUB Pantalla de presentaci¢n
1050   VIEW PRINT 2 TO 24: COLOR , C(CE, 2): CLS : VIEW PRINT
1060   M$ = "Aguarde un instante": GOSUB 940
1070   COLOR C(CE, 3), C(CE, 4)
1080   LOCATE 3, 1
1090   PRINT "N§ Nombre del Trabajador           Puesto       Dias  H.E. H.S.   Salario       ";
1100   FOR I = 4 TO 23
1110     LOCATE I, 1
1120     PRINT "   "
1130   NEXT
1140 RETURN
1150 :
1160 'SUB Linea24
1170  COLOR C(CE, 1), C(CE, 2)
1180  LOCATE 24, 1
1190  PRINT USING "\      \  \        \  Trab.: ###  N¢mina ##:$$#,###,###.##"; TIME$; DATE$; TR;SEM;TOT1!;
1200 RETURN
1210 :
1220 'SUB Mensaje24
1230   IF M = 0 THEN M = 60
1240   LOCATE 24, M
1250   COLOR C(CE, 1), C(CE, 2)
1260   PRINT SPACE$(80 - M);
1270   M = 80 - LEN(M$)
1280   LOCATE 24, M
1290   COLOR C(CE, 7), C(CE, 8)
1300   PRINT M$;
1310 RETURN
1320 :
1330 'SUB MarcaCampo
1340   ON CAMPO GOTO 1350, 1360, 1370
1350   COL = 49: M$ = MASKO$: GOTO 1380
1360   COL = 55: M$ = MASKO$: GOTO 1380
1370   COL = 60: M$ = MASKO$
1380   LOCATE LIN, COL
1390   IF QUITA THEN COLOR C(CE, 1), C(CE, 2) ELSE COLOR C(CE, 7), C(CE, 8)
1400   ON CAMPO GOTO 1410, 1420, 1430
1410   PRINT USING M$; CVI(DIAS$): GOTO 1440
1420   PRINT USING M$; CVI(HE$): GOTO 1440
1430   PRINT USING M$; CVI(HS$)
1440 RETURN
1450 :
1460 'SUB EligeMensajeCampos
1470   ON CAMPO GOTO 1480, 1490, 1500
1480   M$ = "Dias trabajados (1 a 7)": GOTO 1510
1490   M$ = "Horas extras Normales trabajadas (0 a 12)": GOTO 1510
1500   M$ = "Horas extras Especiales trabajadas (0 a ì)"
1510   GOSUB 940
1520 RETURN
1530 :
1540 'SUB Flecha arriba
1550   IF LIN = 4 AND INICIO > 1 THEN INICIO = INICIO - 1: FIN = FIN - 1: REG1 = REG1 - 1: GOSUB 640 ELSE IF LIN = 4 AND INICIO = 1 THEN BEEP: GOTO 590 ELSE IF LIN > 4 THEN QUITA = TRUE: GOSUB 1330: LIN = LIN - 1: REG1 = REG1 - 1
1560   GET #1, REG1
1570   QUITA = FALSE
1580   GOSUB 1330: GOSUB 1160
1590 GOTO 590
1600 :
1610 'SUB Flecha Abajo
1620   IF LIN = 23 AND FIN < TR THEN INICIO = INICIO + 1: FIN = FIN + 1: REG1 = REG1 + 1: GOSUB 640: GOTO 1660
1630   IF LIN = 3 + TR THEN BEEP: GOTO 590
1640   IF LIN = 23 AND FIN = TR THEN BEEP: GOTO 590
1650   IF LIN < 23 THEN QUITA = TRUE: GOSUB 1330: LIN = LIN + 1: REG1 = REG1 + 1
1660   GET #1, REG1
1670   QUITA = FALSE
1680   GOSUB 1330: GOSUB 1160
1690 GOTO 590
1700 :
1710 'SUB Flecha derecha
1720   QUITA = TRUE: GOSUB 1330
1730   CAMPO = CAMPO + 1: IF CAMPO > 3 THEN CAMPO = 1
1740   QUITA = FALSE
1750   GOSUB 1330: GOSUB 1160: GOSUB 1460
1760 GOTO 590
1770 :
1780 'SUB Flecha Izquierda
1790   QUITA = TRUE: GOSUB 1330
1800   CAMPO = CAMPO - 1: IF CAMPO < 1 THEN CAMPO = 3
1810   QUITA = FALSE
1820   GOSUB 1330: GOSUB 1160: GOSUB 1460
1830 GOTO 590
1840 :
1850 'SUB PgUp
1860   IF INICIO = 1 OR TR <= 20 THEN BEEP: GOTO 590
1870   IF INICIO - 20 < 1 THEN REG1 = 1: LIN = 4: INICIO = 1: FIN = 20 ELSE INICIO = INICIO - 20: FIN = FIN - 20: REG1 = REG1 - 20
1880   GOSUB 640
1890   GET #1, REG1
1900   GOSUB 1330: GOSUB 1160
1910 GOTO 590
1920 :
1930 'SUB PgDn
1940   IF FIN = TR OR TR <= 20 THEN BEEP: GOTO 590
1950   IF FIN + 20 > TR THEN REG1 = TR: LIN = 23: INICIO = TR - 19: FIN = TR ELSE INICIO = INICIO + 20: FIN = FIN + 20: REG1 = REG1 + 20
1960   GOSUB 640
1970   GET #1, REG1
1980   GOSUB 1330: GOSUB 1160
1990 GOTO 590
2000 :
2010 'SUB Home
2020   IF INICIO = 1 THEN QUITA = TRUE: GOSUB 1330: REG1 = 1: LIN = 4: CAMPO = 1: QUITA = FALSE: GET #1, REG1: GOSUB 1330: GOSUB 1160: GOSUB 1460: GOTO 590
2030   INICIO = 1: FIN = 20: REG1 = 1: LIN = 4
2040   GOTO 1960
2050 :
2060 'SUB End
2070   IF FIN=TR THEN QUITA=TRUE:GOSUB 1330: REG1=TR: IF TR<=20 THEN LIN=TR+3:CAMPO=1:QUITA=FALSE:GET #1,REG1:GOSUB 1330:GOSUB 1160:GOSUB 1460:GOTO 590 ELSE LIN=23:CAMPO=1:QUITA=FALSE:GET #1,REG1:GOSUB 1330: GOSUB 1160:GOSUB 1460:GOTO 590
2080   INICIO = TR - 19: FIN = TR: REG1 = TR: LIN = 23
2090   GOTO 1960
2100 :
2110 'SUB EditaTexto
2120   M$=" Etiqueta ": GOSUB 1220
2130   LOCATE 2,1: COLOR C(CE,1),C(CE,2): PRINT SPACE$(80);: LOCATE 2,1
2140   IF T$ >= "a" AND T$ <= "z" THEN CADENT$ = CHR$(ASC(T$) - 32) ELSE CADENT$ = T$
2150   IF T$ = "¤" THEN CADENT$ = "¥"
2160   'DO
2170      COLOR C(CE, 1), C(CE, 2)
2180      LOCATE 2, 1
2190      PRINT "> " + CADENT$;: COLOR C(CE,1)+16: PRINT "_ "
2200      GOSUB 900
2210      IF T$=CHR$(8) AND CADENT$<>"" THEN CADENT$=LEFT$(CADENT$,LEN(CADENT$)-1): GOTO 2160 ELSE IF T$=CHR$(8) AND CADENT$="" THEN BEEP: GOTO 2200
2220      IF T$=ENT$ OR LEN(T$)=2 THEN 2340  'Aceptado
2230      IF T$=CHR$(27) THEN CADENT$="": T$="": GOTO 2340  'Aceptado
2240      IF T$=>"A" AND T$<="Z" THEN 2320
2250      IF T$=>"a" AND T$<="z" THEN T$=CHR$(ASC(T$)-32): GOTO 2320
2260      IF T$=" " THEN T$="A": GOTO 2320
2270      IF T$="‚" THEN T$="E": GOTO 2320
2280      IF T$="¡" THEN T$="I": GOTO 2320
2290      IF T$="¢" THEN T$="O": GOTO 2320
2300      IF T$="£" THEN T$="U": GOTO 2320
2310      IF T$="¤" THEN T$="¥"
2320      CADENT$=CADENT$+T$
2330   GOTO 2160
2340   'Aceptado
2350   GOSUB 3390
2360 RETURN
2370 :
2380 'SUB B£squeda
2390   M$=" B£squeda ": GOSUB 1220
2400   J=1
2410   GET #1,J
2420   WHILE FIND$<>LEFT$(NOMBRE$,LEN(FIND$)) AND J<=TR
2430     J=J+1
2440     GET #1,J
2450   WEND
2460   IF FIND$<>LEFT$(NOMBRE$,LEN(FIND$)) THEN GET #1,REG1: M$=" Nombre no localizado, Pulse una tecla ": GOSUB 940: GOSUB 900: LOCATE 2,1: RETURN
2470   IF J>FIN THEN FIN=J: LIN=23: REG1=J: INICIO=FIN-19 ELSE IF J<INICIO THEN INICIO=J: LIN=4: REG1=J: FIN=INICIO+19 ELSE IF REG1>J THEN LIN=LIN-(REG1-J): REG1=J ELSE LIN=LIN+(J-REG1): REG1=J
2480   CAMPO=1: GOSUB 640: GET #1,REG1: GOSUB 1330: GOSUB 1460
2490 RETURN
2500 :
2510 'SUB EditaN£meros
2520   LOCATE 2,1: COLOR C(CE,1),C(CE,2): PRINT SPACE$(80);
2530   M$=" Valor ": GOSUB 1220
2540   CADENT$=T$
2550   LOCATE 2,1
2560     COLOR C(CE,1),C(CE,2)
2570     PRINT "> "+CADENT$;: COLOR C(CE,1)+16: PRINT "_ "
2580     GOSUB 900 'Tecla
2590     IF T$=CHR$(8) AND CADENT$<>"" THEN CADENT$=LEFT$(CADENT$,LEN(CADENT$)-1): GOTO 2550 ELSE IF T$=CHR$(8) AND CADENT$="" THEN BEEP: GOTO 2580
2600     IF T$=CHR$(13) OR LEN(T$)=2 THEN 2640 'Aceptado
2610     IF T$=>"0" AND T$<="9" THEN CADENT$=CADENT$+T$: GOTO 2550
2620     IF T$="." THEN CADENT$=CADENT$+T$: GOTO 2550
2630   BEEP: GOTO 2580
2640   'Aceptado
2650   GOSUB 3390
2660   IF CADENT$="" THEN 590
2670    M$=" Calculando ": GOSUB 1220
2680   IF CAMPO=1 THEN LSET DIAS$=MKI$(VAL(CADENT$)) ELSE IF CAMPO=2 THEN LSET HE$=MKI$(VAL(CADENT$)) ELSE IF CAMPO=3 THEN LSET HS$=MKI$(VAL(CADENT$))
2690   PUT #1,REG1: GET #1,REG1: GOSUB 3760
2700   LSET PAGS$=MKS$(SUELDO!)
2710   LSET PAGC$=MKS$(COMP!)
2720   LSET PAGE$=MKS$(HE!)
2730   LSET TOTAL$=MKS$(TOTAL!)
2740   PUT #1,REG1: GOSUB 4170  'Checa la suma y la imprime
2750   GET #1,REG1
2760   LOCATE LIN,67
2770   COLOR C(CE,1),C(CE,2)
2780   PRINT USING MASKD$;CVS(TOTAL$)
2790   IF T$=CHR$(13) THEN GOSUB 1330: GOTO 590 ELSE 410
2800 :
2810 'SUB BuscaNombre
2820   M$="Tecl‚e el nombre a buscar": GOSUB 940
2830   GOSUB 2110
2840   IF CADENT$="" THEN 590
2850   FIND$=CADENT$
2860   GOSUB 2380: GOSUB 1460
2870 GOTO 590
2880 :
2890 'SUB Imprime
2900 CLOSE
2910 OPEN "R",1,"SEM",2: FIELD #1,2 AS SEM$: LSET SEM$=MKI$(SEM): PUT #1,1:CLOSE
2920 CHAIN "IMPRIME"
2930 :
2940 'ERROR de impresi¢n
2950 RESUME 2960
2960 ON ERROR GOTO 0
2970 M$=" Error ": GOSUB 1220
2980 M$="Error de Impresi¢n, pulse una tecla": GOSUB 940: GOSUB 900
2990 GOSUB 1460: GOTO 590
3000 :
3010 'SUB Baja
3020   M$=" Baja ": GOSUB 1220
3030   M$=" !Precauci¢n­ Trabajador se dar  de baja de esta n¢mina sin opci¢n a recuperar": GOSUB 940
3040   COLOR C(CE,1),C(CE,2): LOCATE 2,1: PRINT SPACE$(80);
3050   LOCATE 2, 1
3060   COLOR C(CE, 5), C(CE, 6)
3070   PRINT " !Trabajador se dar  de baja en esta n¢mina­ ¨Procede? (S/N) "
3080   GOSUB 900
3090   GOSUB 3390
3100   IF T$ <> "S" AND T$ <> "s" THEN GOSUB 1460: GOTO 590
3110   M$ = " En Proceso de Eliminaci¢n ": GOSUB 940
3120   IF TR = 1 THEN CLOSE: KILL ARCH$: RUN "nomina"
3130   OPEN "R",2,"NOMINA.$$$",70
3140   FIELD #2,30 AS N1$,10 AS P1$,4 AS S1$,4 AS C1$,2 AS D1$,2 AS H1$,2 AS H2$,4 AS P2$,4 AS P3$,4 AS P4$,4 AS T1$
3150   REG2 = 0
3160   FOR I = 1 TO TR
3170     GET #1, I
3180     IF I = REG1 THEN 3320
3190     LSET N1$ = NOMBRE$
3200     LSET P1$ = PUESTO$
3210     LSET S1$ = SUELDO$
3220     LSET C1$ = COMP$
3230     LSET D1$ = DIAS$
3240     LSET H1$ = HE$
3250     LSET H2$ = HS$
3260     LSET P2$ = PAGS$
3270     LSET P3$ = PAGC$
3280     LSET P4$ = PAGE$
3290     LSET T1$ = TOTAL$
3300     REG2 = REG2 + 1
3310     PUT #2, REG2
3320   NEXT I
3330   CLOSE
3340   KILL ARCH$
3350   NAME "nomina.$$$" AS ARCH$
3360   OPEN "R",1,"sem": FIELD #1,2 AS SEM$: LSET SEM$=MKI$(SEM): PUT #1,1:CLOSE
3370 RUN 90
3380 :
3390 'SUB LineaAyuda2
3400   COLOR C(CE,1),C(CE,2)
3410   LOCATE 2,1
3420   PRINT "Esc=Salir F7=Imprimir F8=Buscar F9=Borrar                PgUp PgDn Home End "+CHR$(24)+CHR$(25)+CHR$(26)+CHR$(27)
3430 RETURN
3440 :
3450 'SUB L‚eNuevosDatos
3460  M$=" Leyendo y Calculando ": GOSUB 1220
3470  M$="Aguarde un instante, Inicializando los datos de la N¢mina"+STR$(SEM): GOSUB 940
3480  OPEN "R", 2, "TRABAJAD.DAT", 48
3490  FIELD #2, 30 AS N1$, 10 AS P1$, 4 AS S1$, 4 AS C1$
3500  TR = LOF(2) / 48
3510  IF TR = 0 THEN M$="­No hay trabajadores Capturados! Pulse una tecla": GOSUB 940: GOSUB 900: RUN "nomina"
3520  OPEN "R",1,ARCH$,70
3530  FIELD #1,30 AS NOMBRE$,10 AS PUESTO$,4 AS SUELDO$,4 AS COMP$,2 AS DIAS$,2 AS HE$,2 AS HS$,4 AS PAGS$,4 AS PAGC$,4 AS PAGE$,4 AS TOTAL$
3540  FOR I=1 TO TR
3550    GET #2,I
3560    LSET NOMBRE$=N1$
3570    LSET PUESTO$=P1$
3580    LSET SUELDO$=S1$
3590    LSET COMP$=C1$
3600    LSET DIAS$=MKI$(7)
3610    LSET HE$=MKI$(0)
3620    LSET HS$=MKI$(0)
3630    PUT #1,I
3640    GET #1,I
3650    GOSUB 3760 'Calcula Pago
3660    LSET PAGS$=MKS$(SUELDO!)
3670    LSET PAGC$=MKS$(COMP!)
3680    LSET PAGE$=MKS$(HE!)
3690    LSET TOTAL$=MKS$(TOTAL!)
3700    PUT #1,I
3710  NEXT I
3720  CLOSE #2
3730  GOSUB 4170
3740 RETURN
3750 :
3760 'sub CalculaTotal
3770    DESCS!=CVS(SUELDO$)/7+CVS(SUELDO$)
3780    DESCC!=CVS(COMP$)/7+CVS(COMP$)
3790    HE!=(CVS(SUELDO$)/8)*2
3800    HS!=(CVS(SUELDO$)/8)*3
3810    HEC!=(CVS(COMP$)/8)*2
3820    HSC!=(CVS(COMP$)/8)*3
3830    SUELDO!=CSNG(INT(((CVS(SUELDO$)*7)-((7-CVI(DIAS$))*DESCS!)+.5)*100)/100)
3840    COMP!=CSNG(INT(((CVS(COMP$)*7)-((7-CVI(DIAS$))*DESCC!)+.5)*100)/100)
3850    'Estructura el pago de BONOS en m£ltiplos de 1
3860     'IF INT(COMP!)=(COMP!) THEN 3900
3870     'DIF!=INT(COMP!/1): DIF!=COMP!-(DIF!*1)
3880     'COMP!=COMP!-DIF!: SUELDO!=SUELDO!+DIF!
3890    'Contin£a
3900    HE!=CSNG(INT(((HE!*CVI(HE$))+(HEC!*CVI(HE$))+(HS!*CVI(HS$))+(HSC!*CVI(HS$))+.5)*100)/100)
3910    TOTAL!=SUELDO!+COMP!+HE!
3920 RETURN
3930 :
3940 'SUB PideSemana
3950    M$="N£mero de semana por tratar": GOSUB 940
3960    GOSUB 2110 'EditaTexto
3970    IF CADENT$="" THEN RUN "nomina"
3980    IF VAL(CADENT$)<1 OR VAL(CADENT$)>53 THEN M$="Tecl‚e un valor (1 a 53)":GOSUB 940:GOTO 3960
3990    SEM=VAL(CADENT$)
4000    ARCH$="NOMINA."+MID$(STR$(SEM),2)
4010    OPEN "R",1,ARCH$,70
4020    FIELD #1,30 AS NOMBRE$,10 AS PUESTO$,4 AS SUELDO$,4 AS COMP$,2 AS DIAS$,2 AS HE$,2 AS HS$,4 AS PAGS$,4 AS PAGC$,4 AS PAGE$,4 AS TOTAL$
4030    TR=LOF(1)/70
4040    IF FLAG THEN 4140
4050    IF TR=0 THEN CLOSE #1: GOSUB 3450: RETURN
4060    COLOR C(CE,1),C(CE,2): LOCATE 2,1: PRINT SPACE$(80);
4070    LOCATE 2,1
4080    PRINT "!N¢mina ya existe­ Re-elaborar (S/N)"
4090    M$="S borrar  la n¢mina anterior, N o cualquier tecla para modificarla, Esc Aborta": GOSUB 940
4100    GOSUB 900
4110    GOSUB 3390
4120    IF T$=CHR$(27) THEN RUN "Nomina"
4130    IF T$="S" OR T$="s" THEN CLOSE #1: KILL ARCH$: GOSUB 3450: RETURN
4140    GOSUB 4170
4150 RETURN
4160 :
4170 'SUB ChecalaSumaTotal
4180    TOT1!=0
4190    FOR I=1 TO TR
4200      GET #1,I
4210      TOT1!=TOT1!+CVS(TOTAL$)
4220    NEXT
4230    GOSUB 1160 'L¡nea24
4240 RETURN
4250 :
4260 'DATOS de los colores del monitor
4270 '    PPlano Splano Pmenu Smenu Pelec Selec Pcursor Scursor Pmensaje
4280 DATA   7   ,   0  ,  0  ,  7  , 15  ,  0  ,   0   ,   7   ,  15
4290 DATA  14   ,   1  ,  0  ,  3  , 15  ,  6  ,  15   ,   3   ,  14
4300 DATA   1   ,   7  , 15  ,  3  , 15  ,  1  ,  11   ,   7   ,  14
4310 DATA   0   ,   7  , 14  ,  2  , 15  ,  0  ,  15   ,   2   ,  12
4320 DATA   0   ,   7  , 14  ,  4  , 15  ,  0  ,  15   ,   4   ,  15
4330 DATA   8   ,   7  , 15  ,  5  , 13  ,  0  ,  15   ,   5   ,  14
